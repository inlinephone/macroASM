;Лабораторная работа №6, студент Чирцов Н., ИКБО-3-15 Вариант 3
;Номер первого положительного
;r0 - счётчик первого положиетльного числа
;r1,r2,r3 - используются в цикле вывода;
;r4 - определяет ввод пробела
;r5 - опепределяет ввод минуса
;r6 - защита от повторного ввода минуса
;r7 - определение ввода нуля
include	"lib"
lab6:	start	0
;==============Вывод начального текста и массива===================
Circlevyvstr:	
	vyvstr	text,textlen
vvod:	
	vyvstr	text3,text3len
	ld	r0,#1	;счётчик
	ld	r4,#2	;0 - пробела не было
	clear	r5	;0 - минуса не было
	clear	r6	;0 - положительного числа не было
	ld	r7,#-1	;0 - есть цифра не ноль
;===============Ввод массива и фильтрование вводимых символов=======
readfiltr:	rd	#1
	comp	a,#45	;минус
	jeq	minus
	comp	a,#32	;пробел
	jeq	space
	comp	a,#13	;enter
	jeq	enter
	comp 	a,#48	;от '0'
	jlt	readfiltr
	comp 	a,#57	;до '9'
	jgt	readfiltr
	comp	r4,#0	;если пробела нет
	jeq	cont
	comp	r5,#1	;если был минус
	jeq	cont
	ld	r4,#0
	comp	a,#48	;если был ноль
	jeq	zero
	ld	r7,#0	;появилась не нулевая цифра
	ld	r6,#1	;положительное число
zero:	wd	#0
	comp	r7,#0	;было ли другое число кроме нуля
	jeq	readfiltr
	ld	r7,#1	;были пока только нули
	jmp	readfiltr
cont:	ld	r4,#0	;возвращает возможность увеличения счётчика при вводе пробела
	ld	r5,#0	;можно вводить минус
	wd	#0
	jmp	readfiltr
minus:	comp	r5,#1	;был ли до этого введён минус
	jeq	readfiltr
	comp	r4,#1	;был ли до этого введён пробел
	jlt	readfiltr
	wd	#0
	ld	r5,#1	;был введён минус
	jmp	readfiltr
space:	comp	r5,#1	;если предыдущий символ минус - пробел не выводится
	jeq	readfiltr
	wd	#0
	comp 	r4,#0	;был ли до этого введён пробел (0 - нет)
	ld	r4,#1
	jeq	nomer	;тогда переход на увеличение счётчика
	ld	r7,#-1	;нулей не было
	jmp	readfiltr
enter:	comp	r5,#1	;запрет ввода enter, если введён только минус
	jeq	readfiltr
	endl		;перевод строки
	comp	r6,#1	;если есть положительное число, то выводим его номер
	jeq	vyvnomer
	jmp	netpol			
;===============Вычисление номера элемента=========================	
nomer:	comp	r7,#1	;если число только из нулей, то можно увеличить индекс
	jeq	nm
	comp	r6,#1	;если уже было положительное число, счётчик не увеличивается
	jeq	readfiltr
nm:	add 	r0,#1	;увеличение счётчика
	jmp	readfiltr
;=================Вывод номера элемента=============================
vyvnomer:	
	vyvstr	text1,text1len	;вызов п/п вывода строки
	mov	a,r0
	outchislo		;вывод номера
	endl		;перевод строки
	jmp	exit
;================Вывод 'Нет положительных'=======================
netpol:	
	vyvstr	text2,text2len	;вызов п/п
	jmp	exit
;=============Запрос на продолжение выполнения программы==========================	
exit:	
	vyvstr	text4,text4len
	rd	#1
	wd	#0
	comp	a,#121	;если введён 'y', то в начало программы
	jeq	vvod
	comp	a,#110	;если введён 'n', то в выйти из программы
	jeq	return
	endl		;перевод строки
	jmp	exit
	
return:	ret

;=========Данные===========
text:	byte	'Лабораторная работа №6, Чирцов Н., ИКБО-3-15, Вариант 3',10,13,'Номер первого положительного'
textlen:	equ	*-text
text1:	byte	'Номер первого положительного = '
text1len:	equ	*-text1	
text2:	byte	'Нет положительных!',10,13
text2len:	equ	*-text2
text3:	byte	10,13,'Введите целые числа, разделяя их пробелом',10,13,'Массив: '
text3len:	equ	*-text3
text4:	byte	'Продолжить тестирование? y/n',10,13
text4len:	equ	*-text4

	end